# vim: ts=2 sw=2
# - Try to find the required XXXX components(default: COMPONENT_XX)
#
# Once done this will define
#  XXXX_FOUND         - System has the all required components.
#  XXXX_INCLUDE_DIRS  - Include directory necessary for using the required components headers.
#  XXXX_LIBRARIES     - Link these to use the required XXXX components.
#  XXXX_DEFINITIONS   - Compiler switches required for using the required XXXX components.
#
# For each of the components it will additionally set.
#   - COMPONENT_XX
# the following variables will be defined
#  <component>_FOUND        - System has <component>
#  <component>_INCLUDE_DIRS - Include directory necessary for using the <component> headers
#  <component>_LIBRARIES    - Link these to use <component>
#  <component>_DEFINITIONS  - Compiler switches required for using <component>
#  <component>_VERSION      - The components version
#
#
# Redistribution and use is allowed according to the terms of the BSD license.
# For details see the accompanying COPYING-CMAKE-SCRIPTS file.

include(FindPackageHandleStandardArgs)

# The default components were taken from a survey over other FindXXXX.cmake files
if (NOT XXXX_FIND_COMPONENTS)
  set(XXXX_FIND_COMPONENTS COMPONENT_XX)
endif ()

#
### Macro: set_component_found
#
# Marks the given component as found if both *_LIBRARIES AND *_INCLUDE_DIRS is present.
#
macro(set_component_found _component )
  if (${_component}_LIBRARIES AND ${_component}_INCLUDE_DIRS)
    # message(STATUS "  - ${_component} found.")
    set(${_component}_FOUND TRUE)
  else ()
    # message(STATUS "  - ${_component} not found.")
  endif ()
endmacro()

#
### Macro: find_component
#
# Checks for the given component by invoking pkgconfig and then looking up the libraries and
# include directories.
#
macro(find_component _component _pkgconfig _library _header)

  if (NOT WIN32)
     # use pkg-config to get the directories and then use these values
     # in the FIND_PATH() and FIND_LIBRARY() calls
     find_package(PkgConfig)
     if (PKG_CONFIG_FOUND)
       pkg_check_modules(PC_${_component} ${_pkgconfig})
     endif ()
  endif (NOT WIN32)

  find_path(${_component}_INCLUDE_DIRS ${_header}
    HINTS
      ${PC_${_component}_INCLUDEDIR}
      ${PC_${_component}_INCLUDE_DIRS}
    PATH_SUFFIXES
      XXXX
  )

  find_library(${_component}_LIBRARIES NAMES ${_library}
      HINTS
      ${PC_${_component}_LIBDIR}
      ${PC_${_component}_LIBRARY_DIRS}
  )

  set(${_component}_DEFINITIONS  ${PC_${_component}_CFLAGS_OTHER} CACHE STRING "The ${_component} CFLAGS.")
  set(${_component}_VERSION      ${PC_${_component}_VERSION}      CACHE STRING "The ${_component} version number.")

  set_component_found(${_component})

  mark_as_advanced(
    ${_component}_INCLUDE_DIRS
    ${_component}_LIBRARIES
    ${_component}_DEFINITIONS
    ${_component}_VERSION)

endmacro()


# Check for cached results. If there are skip the costly part.
if (NOT XXXX_LIBRARIES)

  # Check for all possible component.
  find_component(COMPONENT_XX    libxxx    xxx  /component_header_dir/xxx.h)

  # Check if the required components were found and add their stuff to the XXXX_* vars.
  foreach (_component ${XXXX_FIND_COMPONENTS})
    if (${_component}_FOUND)
      # message(STATUS "Required component ${_component} present.")
      set(XXXX_LIBRARIES   ${XXXX_LIBRARIES}   ${${_component}_LIBRARIES})
      set(XXXX_DEFINITIONS ${XXXX_DEFINITIONS} ${${_component}_DEFINITIONS})
      list(APPEND XXXX_INCLUDE_DIRS ${${_component}_INCLUDE_DIRS})
    else ()
      # message(STATUS "Required component ${_component} missing.")
    endif ()
  endforeach ()

  # Build the include path with duplicates removed.
  if (XXXX_INCLUDE_DIRS)
    list(REMOVE_DUPLICATES XXXX_INCLUDE_DIRS)
  endif ()

  # cache the vars.
  set(XXXX_INCLUDE_DIRS ${XXXX_INCLUDE_DIRS} CACHE STRING "The XXXX include directories." FORCE)
  set(XXXX_LIBRARIES    ${XXXX_LIBRARIES}    CACHE STRING "The XXXX libraries." FORCE)
  set(XXXX_DEFINITIONS  ${XXXX_DEFINITIONS}  CACHE STRING "The XXXX cflags." FORCE)

  mark_as_advanced(XXXX_INCLUDE_DIRS
                   XXXX_LIBRARIES
                   XXXX_DEFINITIONS)

endif ()

# Now set the noncached _FOUND vars for the components.
foreach (_component COMPONENT_XX)
  set_component_found(${_component})
endforeach ()

# Compile the list of required vars
set(_XXXX_REQUIRED_VARS XXXX_LIBRARIES XXXX_INCLUDE_DIRS)
foreach (_component ${XXXX_FIND_COMPONENTS})
  list(APPEND _XXXX_REQUIRED_VARS ${_component}_LIBRARIES ${_component}_INCLUDE_DIRS)
endforeach ()

# Give a nice error message if some of the required vars are missing.
find_package_handle_standard_args(XXXX DEFAULT_MSG ${_XXXX_REQUIRED_VARS})
